<!--
 *  Bayrell Time Planner
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
-->

<class version="1.0"
	name="Bayrell.TimePlanner.Admin.AdminProjects" extends="Runtime.Web.CRUD.CrudPage"
	module="Bayrell.TimePlanner" model="Bayrell.TimePlanner.Admin.CrudPageModel"
>

<use name="Runtime.Exceptions.RuntimeException" />
<use name="Runtime.Core.Message" />
<use name="Runtime.Core.RemoteCallAnswer" />
<use name="Runtime.Web.LayoutModel" />
<use name="Runtime.Web.RenderContainer" />
<use name="Runtime.Web.RenderDriver" />
<use name="Runtime.Web.Route" />
<use name="Runtime.Web.RouteList" />
<use name="Runtime.Web.RouteMiddleware" />
<use name="Runtime.Web.Events.MouseClickEvent" />
<use name="Runtime.Web.CRUD.FieldInfo" />
<use name="Runtime.Web.CRUD.Form" />
<use name="Runtime.Web.Input.Input" component="true" />
<use name="Runtime.Web.Input.Label" component="true" />
<use name="Runtime.Web.Input.Select" component="true" />
<use name="Runtime.Web.Input.SelectText" component="true" />
<use name="Runtime.Web.Input.TextArea" component="true" />
<use name="Runtime.Web.Table.TableModel" />


<script>


/**
 * Returns object name
 */
pure string getCrudObjectName() => "Bayrell.TimePlanner.Admin.Project";



/**
 * Route Action
 * @return RenderContainer
 */
@Route{ "uri": "/admin/projects/", "name": "app.admin.projects" }
@RouteMiddleware{ "value": "Runtime.Web.Auth.AuthFrontend::checkAuthMiddleware" }
static async RenderContainer actionIndex(RenderContainer container)
{
	/* Create model */
	CrudPageModel page_model = await CrudPageModel::crudSearch
	(
		{
			"object_name": static::getCrudObjectName(),
			"interface_name": "core.crud",
			"method_name": "search",
		},
		container
	);
	
	/* Set title */
	container <= layout <= title <= "Projects page";
	container <= layout <= layout_name <= "admin";
	
	/* Set model */
	container <= layout <= page_class <= classof AdminProjects;
	container <= layout <= page_model <= page_model;
	
	return [ container ];
}



/**
 * Returns options
 */
pure Collection getOptions(LayoutModel layout, CrudPageModel model, Dict params, string name = "")
{
	return parent::getOptions(layout, model, params, name);
}



/**
 * Returns crud struct
 */
pure Collection<FieldInfo> getStruct(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	new FieldInfo
	{
		"api_name": "project_id",
		"primary": true,
	},
	
	new FieldInfo
	{
		"api_name": "number",
		"label": "",
		"class_name": classof Label,
		"class_settings":
		{
			"render": method static::renderNumber,
		},
	},
	
	new FieldInfo
	{
		"api_name": "project_name",
		"label": "Name",
		"class_name": classof Input,
		"class_name_table": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "edit-buttons",
		"label": "",
		"class_name": classof Label,
		"class_settings":
		{
			"render": method static::renderButtons,
		},
	}
];



/**
 * Returns filter fields
 */
pure Collection<string> getFilterFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"project_name",
];



/**
 * Returns form fields
 */
pure Collection<string> getFormFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"project_name",
];



/**
 * Returns table fields
 */
pure Collection<string> getTableFields(LayoutModel layout, CrudPageModel model, Dict params) =>
[
	"number",
	"project_name",
	"edit-buttons",
];



/**
 * Returns messages
 */
pure Dict<string> getMessages(LayoutModel layout, CrudPageModel model, Dict params) =>
{
	"add": _("Bayrell.CloudOS", "Add project"),
	"delete": string (Dict item) =>
		_("Runtime.Web.CRUD", "Do you realy want to delete '%name%' ?", { "name": item["project_name"] } ),	
};



/**
 * Dialog settings
 */
pure Dict getDialogSettings(LayoutModel layout, CrudPageModel model, Dict params) =>
{
};



/**
 * Form settings
 */
pure Dict getFormSettings(LayoutModel layout, CrudPageModel model, Dict params) =>
{
	"class_name": classof Form,
};

</script>



</class>